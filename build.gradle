plugins {
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.spongepowered.mixin'
    id 'org.parchmentmc.librarian.forgegradle'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

archivesBaseName = project.mod_name
version = project.mod_version
group = project.maven_group

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: 'parchment', version: "${parchment_version}-${minecraft_version}"

    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {

        client {
            workingDirectory project.file('run')

            arg "-mixin.config=${project.mod_id}.mixins.json"

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', project.mod_id

            mods {
                "${project.mod_id}" {
                    source sourceSets.main
                }
            }
        }

        server {

            workingDirectory project.file('run')

            arg "-mixin.config=${project.mod_id}.mixins.json"

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', project.mod_id

            mods {
                "${project.mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', project.mod_id,
                '--all', '--output', file('src/generated/resources/'),
                '--existing', file('src/main/resources/')

            mods {
                "${project.mod_id}" {
                    source sourceSets.main
                }
            }
        }

    }
}

repositories {
    maven {
        name "JitPack"
        url 'https://jitpack.io'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-common:${mixin_extras_version}"))
    implementation(jarJar("com.github.llamalad7.mixinextras:mixinextras-forge:${mixin_extras_version}")) {
        jarJar.ranged(it, "[${mixin_extras_version},)")
    }

    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"

    debug.verbose = true
    debug.export = true
}

java {
    withSourcesJar()
}

processResources {
    inputs.property "version", project.mod_version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.mod_version
    }
}

void processJar(Jar jarTask) {
    jarTask.configure {
        from "LICENSE_FABRIC"
        from "LICENSE_FORGE"

        manifest {
            attributes([
                "Specification-Title"     : "${mod_name}",
                "Specification-Vendor"    : "PinkGoosik",
                "Specification-Version"   : "${mod_version}",
                "Implementation-Title"    : "${mod_id}-forge",
                "Implementation-Version"  : "1",
                "Implementation-Vendor"   : "LimonBlaze",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "${mod_id}.mixins.json"
            ])
        }
    }
}

jar {
    archiveClassifier.set('slim')
    finalizedBy 'reobfJar'
}

tasks.jarJar {
    archiveClassifier.set("")
    finalizedBy reobf.maybeCreate('jarJar')
}

processJar(jar)
processJar(tasks.jarJar)

publishMods {
    file = tasks.jarJar.archiveFile
    changelog = file("changelog/${mod_version}.md").text
    version = mod_version
    type = STABLE
    displayName = "[Forge]Visuality: Reforged ${mod_version} for ${minecraft_version}"
    modLoaders.add("forge")

    curseforge {
        projectId = "704256"
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.add(minecraft_version)
    }

    modrinth {
        projectId = "z13R7Et1"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add(minecraft_version)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            fg.component(it)
        }
    }
    repositories {
        mavenLocal()
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

// Merge the resources and classes into the same directory.
// This is done because java expects modules to be in a single directory.
// And if we have it in multiple we have to do performance intensive hacks like having the UnionFileSystem
// This will eventually be migrated to ForgeGradle so modders don't need to manually do it. But that is later.
sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
